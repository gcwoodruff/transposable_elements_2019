initialize() {
initializeTreeSeq();
	defineConstant("L", 3e6);               // chromosome length
	defineConstant("teInitialCount", 1000);  // initial number of TEs
	defineConstant("teJumpP", 0.0001);      // TE jump probability
	defineConstant("teDisableP", 0.00005);  // disabling mut probability
	
	initializeMutationRate(0.0);
	initializeMutationType("m1", 0.5, "f", 0.0);  // neutral
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, L-1);
        initializeRecombinationRate(c(1e-8,1e-10,1e-8),c(asInteger((L-1)/3),asInteger(2*(L-1)/3),L-1));
	
	// transposon mutation type; also neutral
	initializeMutationType("m2", 0.5, "f", 0.0);
	m2.convertToSubstitution = F;
	
	// disabled transposon mutation type;
	initializeMutationType("m3", 0.5, "f", 0.0);
	m3.convertToSubstitution = F;


}
1 late() {
	sim.addSubpop("p1", 5000);
	
	sim.tag = 0;	// the next unique tag value to use for TEs
	
	// create some transposons at random positions
	genomes = sim.subpopulations.genomes;
	positions = rdunif(teInitialCount, 0, L-1);
	
	for (teIndex in 0:(teInitialCount-1))
	{
		pos = positions[teIndex];
		mut = genomes.addNewDrawnMutation(m2, pos);
		mut.tag = sim.tag;
		sim.tag = sim.tag + 1;
		//selection is dependent on position		
		if (mut.position >=asInteger((L-1)/3) & mut.position<=asInteger(2*(L-1)/3) ){
			mut.setSelectionCoeff(rgamma(1, -0.003,0.3));
		}
	}
}
modifyChild() {
	// disable transposons with rate teDisableP
	for (genome in child.genomes)
	{
		tes = genome.mutationsOfType(m2);
		teCount = tes.size();
		mutatedCount = teCount ? rpois(1, teCount * teDisableP) else 0;
		
		if (mutatedCount)
		{
			mutatedTEs = sample(tes, mutatedCount);
			
			for (te in mutatedTEs)
			{
				all_disabledTEs = sim.mutationsOfType(m3);
				disabledTE = all_disabledTEs[all_disabledTEs.tag == te.tag];
				
				if (size(disabledTE))
				{
					// use the existing disabled TE mutation
					genome.removeMutations(te);
					genome.addMutations(disabledTE);
					next;
				}
				
				// make a new disabled TE mutation with the right tag
				genome.removeMutations(te);
				disabledTE = genome.addNewDrawnMutation(m3, te.position);
				disabledTE.tag = te.tag;
				if (te.position >=asInteger((L-1)/3) & te.position<=asInteger(2*(L-1)/3) )
					te.setSelectionCoeff(rgamma(1, -0.003,0.3));
			}
		}
	}
	
	return T;
}


late() {
	// make active transposons copy themselves with rate teJumpP
	for (individual in sim.subpopulations.individuals)
	{
		for (genome in individual.genomes)
		{
			tes = genome.mutationsOfType(m2);
			teCount = tes.size();
			jumpCount = teCount ? rpois(1, teCount * teJumpP) else 0;
			
			if (jumpCount)
			{
				jumpTEs = sample(tes, jumpCount);
				
				for (te in jumpTEs)
				{
					// make a new TE mutation
					pos = rdunif(1, 0, L-1);
					jumpTE = genome.addNewDrawnMutation(m2, pos);
					jumpTE.tag = sim.tag;
					sim.tag = sim.tag + 1;
					if (jumpTE.position >=asInteger((L-1)/3) & jumpTE.position<=asInteger(2*(L-1)/3) )
						jumpTE.setSelectionCoeff(rgamma(1, -0.003,0.3));
				}
			}
		}
	}
}
50000 late() {
	// print information on each TE, including the fraction of it disabled
	all_tes = sortBy(sim.mutationsOfType(m2), "position");
	all_disabledTEs = sortBy(sim.mutationsOfType(m3), "position");
	
	//count number of active and disabled it region of high and low recombination
	DISARM=length(all_disabledTEs[all_disabledTEs.position<=asInteger((L-1)/3) |all_disabledTEs.position>=asInteger(2*(L-1)/3)])/2;
	DISCENT=length(all_disabledTEs)-length(all_disabledTEs[all_disabledTEs.position<=asInteger((L-1)/3) |all_disabledTEs.position>=asInteger(2*(L-1)/3)]);
	
	ACTARM=length(all_tes[all_tes.position<=asInteger((L-1)/3) |all_tes.position>=asInteger(2*(L-1)/3)])/2;
	ACTCENT=length(all_tes)-length(all_tes[all_tes.position<=asInteger((L-1)/3) |all_tes.position>=asInteger(2*(L-1)/3)]);
	
	TEfile=paste0(getSeed()+"_TE_DISARM_DISCENT_ACTARM_ACTCENT_DOM_SEL_CENTR_WEAK.txt");
	line=paste0(TEfile + " " + DISARM + " " + DISCENT + " " + ACTARM + " " + ACTCENT);
	
	
	writeFile(TEfile, line, append=T);
	OUT=paste0(getSeed()+"_output_full_DOM_sel_center_weak.txt");
	
	sim.outputFull(filePath=OUT,ages=T);


}

